plugins {
    //Не обновлять версию 'org.openapi.generator' пока проблема с @RequestParam не будет решена
    //https://github.com/OpenAPITools/openapi-generator/issues/3248
    id "org.openapi.generator" version "4.1.3"
}

def rootDirToGenerate = "$projectDir/../".toString()
/**
 * https://github.com/OpenAPITools/openapi-generator/tree/master/modules/openapi-generator-gradle-plugin
 */
task generateCode(type: org.openapitools.generator.gradle.plugin.tasks.GenerateTask/*, dependsOn: 'cleanOldClasses'*/) {
    group = "openapi tools"

    doFirst {//clean old classes
        delete "$rootDirToGenerate/src/main/java/com/varb/schedule/buisness/logic/controllers/api"
        delete "$rootDirToGenerate/src/main/java/com/varb/schedule/buisness/models/dto"
    }

    generatorName = "spring"
    inputSpec = "$rootDirToGenerate/../api/depSchedule.yml".toString()
    outputDir = rootDirToGenerate
    print("outputDir=" + outputDir.toString())
    skipOverwrite = false
    invokerPackage = "com.varb.schedule.buisness"
    apiPackage = "com.varb.schedule.buisness.logic.controllers.api"
    modelPackage = "com.varb.schedule.buisness.models.dto"
    modelNameSuffix = "Dto"
    generateModelTests = false
    generateApiTests = false
    /**
     * https://github.com/OpenAPITools/openapi-generator/blob/master/docs/generators/spring.md
     */
    configOptions = [
            dateLibrary             : "java8",
            interfaceOnly           : true,
            ensureUniqueParams      : true,
            sortParamsByRequiredFlag: true,
            returnSuccessCode       : true,
            useOptional             : true,
            performBeanValidation   : true
    ]

    doLast { // delete unnecessary files
        delete "$rootDirToGenerate/pom.xml"
        delete "$rootDirToGenerate/.openapi-generator-ignore"
        delete "$rootDirToGenerate/.openapi-generator"
        delete "$rootDirToGenerate/README.md"
    }
}
